SakuraMenuGreetings : nonoverlap
{
	"Hello."
	"How can I help you?"
	"What do you need?"
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\0\s[0]\b[2]\![set,autoscroll,disable]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	"\n\n\_q"
	
	"\![*]\q[Talk to me,OnAiTalk]  "
	switch LastTalk == "" //Uses top option if there is a dialogue to repeat
	{
		"\![*]\q[Say that again,OnLastTalk]"
		"\![*]\f[color,disable]Say that again\f[color,default]"
	}
	
	"\n\n"
	"\![*]\q[Ask questions,OnAskStuff]\n\n"
	"\![*]\q[Play Rock-Paper-Scissors,OnRPSMenu]\n\n"
	"\![*]\q[Change talk rate,OnTalkRateMenu]\n\n"
	
	"\![*]\q[Cancel,blank]"	
}

OnLastTalk
{
	LastTalk
}

OnTalkRateMenu : all
{
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	elseif aitalkinterval == 900; _talkrate = "15 minutes"
	elseif aitalkinterval == 1200; _talkrate = "20 minutes"

	"\0\s[0]\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"

	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\![*]\q[15 minutes,OnTalkRateChange,900]\n"
	"\![*]\q[20 minutes,OnTalkRateChange,1200]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Cancel,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
}

OnAskStuff
{
	"\_q\b2What do you want to ask?\n\n"
	--
	"\![*]\q[Why are you named Janet?,OnJanetName]\n/
	\![*]\q[What do you fairies do all day?,OnFairyJob]\n/
	\![*]\q[Have you ever gone to space?,OnSpaceFairies]\n\n[half]/
 	\![*]\q[Back,OnSakuraMenu]\e"
}

OnJanetName
{
	"\*\0\s[0]Glad you asked! I do have an unusual name. Most fairies are named after flowers they favor, or that their personality is like. Part of it is because I'm a dragonfly fairy, not a butterfly fairy like my friend Violet. Also, not very many flower names felt right for me. It came about due to my parents seeing Mars in the sky one fortunate evening. It was a week before I was born, too! They thought of the words, Mars, Planet, then Janet! That's how I got my name.\s[5] I think it's very lovely.\n\n/
	\![*]\q[Back,OnAskStuff]"
}

OnFairyJob
{
	"\*\0\s[0]We fairies are usually quite busy all day, and some of us are even busy at night! We work hard taking care of flowers and plants,\w5\s[3] shooing away pests,\w5\s[0] talking to flowers, making sure the soil is just right, painting some of the flowers correctly to ensure they look pretty, and more! But we also have many fun games to play from time to time, like chase, rock paper scissors, art contests, and many others.\n\n/
	\![*]\q[Back,OnAskStuff]"
}

OnSpaceFairies
{
	"\*\0\s[2]Haha! \s[5]What a fun and out-of-this-world question! \s[0]None of us, as far as I know, have gone to space, but Bluebell has longed to travel to space, and others have at least been fascinated by the stars. I myself was named after my parents saw Mars in the sky, so I'd be interested, but concerned about how to survive going into space. I've heard it's pretty difficult! But I'd love to visit space someday!\n\n/
	\![*]\q[Back,OnAskStuff]"
}

OnRPSMenu
{
	"\_q\0\s[0]Do you want to play Rock-Paper-Scissors with me, or want to have me play the game with Violet?\n\n"
	--
	"\![*]\q[Play with Janet,OnStartRPS]\n/
	\![*]\q[Have Janet play with Violet,fairybets]\n\n/
	\![*]\q[Cancel,OnSakuraMenu]\e"
}

Select.fairybets
{
	"\![*]\q[Rules,laws]\n\n/
	\![*]\q[Start,challenge]\n\n/
	\![*]\q[Back,OnRPSMenu]\e"
}

Select.laws
{
	"\0\s[0]Okay! We'll play Rock, Paper, Scissors with each other. Pick a fairy, and see who wins. If your fairy wins, you win!\n\n\_q"
	--
	"\![*]\q[Start,challenge]\n\n/
	\![*]\q[Back,OnRPSMenu]\e"
}

Select.challenge
{
	"\0\s[0]Rock, \w8\1\s[10]paper, \w8\0\s[0]scissors, \w8\1\s[12]SHOOT! \s[10]\x\c\w8\0\s[0]I chose "
	--
	"rock."
	"paper."
	"scissors."
	--
	"\w8\n\n[half]\1\s[10]I chose "
	--
	"rock."
	"paper."
	"scissors."
	--
	"\e"
}

//Written by Zichqec https://ukagaka.zichqec.com/index.html

OnStartRPS
{
	"\0\s[0]Alright, let's play!"
	"\0\s[0]You're on!"
	--
	"\w8\w8 Rock,\w5 paper,\w5 scissors..."
	--
	"\_q\n\n/
	\![*]\q[Rock,OnRPS,rock,%(reference1),%(reference2),%(reference3),%(reference4)]\n/
	\![*]\q[Paper,OnRPS,paper,%(reference1),%(reference2),%(reference3),%(reference4)]\n/
	\![*]\q[Scissors,OnRPS,scissors,%(reference1),%(reference2),%(reference3),%(reference4)]\n\n/
	\![*]\q[Close,OnRPSMenu]\e"
}

OnRPS
{
	//If these values are empty, set them to 0. These count the number of matches, number of wins, number of losses, and number of draws, respectively
	if reference1 == ""; reference1 = 0
	if reference2 == ""; reference2 = 0
	if reference3 == ""; reference3 = 0
	if reference4 == ""; reference4 = 0

	_userthrow = reference0 //Gets the user's pick
	_ghostthrow = ANY("rock,paper,scissors") //Gets the ghost's pick. ANY chooses randomly from the comma-separated list

	//Figures out who won
	_winlose = ""
	if _userthrow == _ghostthrow; _winlose = "tie"
	elseif _userthrow == "rock" && _ghostthrow == "scissors"; _winlose = "win"
	elseif _userthrow == "paper" && _ghostthrow == "rock"; _winlose = "win"
	elseif _userthrow == "scissors" && _ghostthrow == "paper"; _winlose = "win"
	else; _winlose = "lose"

	//Figures out what surface the ghost should be using. Put your surfaces in here.
	_pose = ""
	if _winlose == "lose" //If the ghost won
	{
		if _ghostthrow == "rock"; _pose = "\s[0]"
		elseif _ghostthrow == "paper"; _pose = "\s[0]"
		else; _pose = "\s[0]" //Scissors
	}
	else //If the user won or it's a tie. You can make another elseif if you want these to be different.
	{
		if _ghostthrow == "rock"; _pose = "\s[0]"
		elseif _ghostthrow == "paper"; _pose = "\s[0]"
		else; _pose = "\s[0]" //Scissors
	}

	//Gets the throw that won. If it's a draw, it'll default to the ghost, but that won't matter because it won't be called.
	_winthrow = _ghostthrow
	if _winlose == "win"; _winthrow = _userthrow

	//Phrases to add extra spice to dialogue
	_winreason = ""
	if _winthrow == "rock"; _winreason = "rock smashes scissors"
	elseif _winthrow == "paper"; _winreason = "paper covers rock"
	else; _winreason = "scissors cuts up paper"

	//Note - If you want to have the winreason or the throws be capitalized, you can use my capitalize function like so: %(Capitalize(_winreason))
	"\0%(_pose)" //Sets up the appropriate pose
	--
	reference1++ //Count of matches
	if _winlose == "win" //If the user won
	{
		reference2++ //Count of wins
		"%(Capitalize(_userthrow)) beats %(_ghostthrow),\w4 you got me!"
		"Oh, %(_winreason),\w4 I lost!"
	}
	elseif _winlose == "tie" //If it was a tie
	{
		reference4++ //Count of ties
		"Oh, looks like we both picked %(_userthrow).\w8\w8 It's a draw."
		"Oh, we picked the same thing.\w4 It's a draw."
	}
	else //If the ghost won
	{
		reference3++ //Count of losses
		"%(Capitalize(_ghostthrow)) beats %(_userthrow)!\w8\w8 I win!"
		"Got you!\w8\w8 %(Capitalize(_winreason))!"
	}
	--
	"\w8\w8 " //A pause and space between the dialogues. Remove or change if you like.
	--
	//You can use _winlose to make special comments for specific outcomes, if you want. It's 'win' if the user won, 'lose' if the ghost won, and 'tie' if it was a draw
	"\s[0]Let's go again!"
	"\s[0]Want to play again?"
	if _winlose == "win"; "\s[0]Let's play another round,\w4 I want to beat you this time!"
	elseif _winlose == "lose"; "\s[0]Let's play another round,\w4 I want to beat you again!"
	--
	"\_q\n\n/
	Matches: %(reference1)  Won: %(reference2)  Lost: %(reference3)  Ties: %(reference4)\n/
	\![*]\q[Another round!,OnStartRPS,Dummy value,%(reference1),%(reference2),%(reference3),%(reference4)]\n/
	\![*]\q[Close,OnRPSMenu]\e"
}
